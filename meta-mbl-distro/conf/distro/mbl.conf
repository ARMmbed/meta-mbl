# Copyright (c) 2018 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

###############################################################################
# mbl.conf
#
# This file is the mbed linux OpenEmbedded distribution configuration file.
# The symbol definitions control the behaviour of the meta layers composed
# to form the distribution, including:
# - Enabling the distribution virtualisation features.
# - Removing the x11 feature which is enabled by default.
# - Specifying virtual providers for subsystems.
###############################################################################

require conf/distro/mbl-partitions.conf
require conf/distro/mbl-directories.conf
require conf/distro/mbl-linked-repositories.conf
require conf/distro/include/mbl-default.conf

DISTRO_NAME = "Mbed Linux OS"
DISTRO_VERSION = "0.0"
DISTRO_FEATURES_append = " virtualization"

# Set systemd as the init manager
DISTRO_FEATURES_append = " systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"Â 
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit"

# Enable build history with installed package size information and image info.
INHERIT += "buildhistory"
INHERIT += "image-buildinfo"
BUILDHISTORY_COMMIT = "1"

IMAGE_FSTYPES = "ext4 wic.gz wic.bmap tar.xz"

# See MBL_FIT_IMAGE_BOOT_FILES_DIR comments in mbl-default.conf for more
# details.
IMAGE_BOOT_FILES += "${MBL_FIT_IMAGE_BOOT_FILES_DIR}/*"

# Prevent OE from messing around too much with /var/log - we have a separate
# partition for it
VOLATILE_LOG_DIR = "no"

# Remove x11 related packages for distro images.
DISTRO_FEATURES_DEFAULT_remove = "x11"

# dosfstools is in RRECOMMENDS for packagegroup-base-vfat but it's GPLv3 so we
# don't want it
BAD_RECOMMENDATIONS += "dosfstools"

# docker and lxc is in RRECOMMENDS for runc-opencontainers package - we do not need it
# We would like to have the minimal runc functionality
BAD_RECOMMENDATIONS += "docker lxc"

# readline is GPLv3 so don't include the command line tools for bluez5 that
# depend on readline
PACKAGECONFIG_remove_pn-bluez5 = "readline"

# By default wpa-supplicant uses gnutls as its TLS library, but gnutls depends
# on libunistring which is GPLv3, so tell wpa-supplicant to use openssl instead
# of gnutls.
PACKAGECONFIG_pn-wpa-supplicant = "openssl"

# Defines root hash suffix.
ROOT_HASH_SUFFIX  = ".root_hash.txt"
#
# Defines signed root hash suffix.
SIGNED_ROOT_HASH_SUFFIX = ".sha256"
#
# Defines base 64 suffix used for the signed root hash
BASE64_SUFFIX = ".base64"
#
# Defines directory for the Verity keys
VERITY_KEYS_DIR = "${MBL_DISTRO_LAYER_VERITY_KEYS_DIR}"

# Defines rootfs verity public key name
VERITY_ROOTFS_ROOT_HASH_PUBLIC_KEY_NAME="verity_rootfs_root_hash_public.pem"
#
# Defines rootfs verity private key name
VERITY_ROOTFS_ROOT_HASH_PRIVATE_KEY_NAME="verity_rootfs_root_hash_private.pem"

# Set runc preferred provider to Open Container Initiative (OCI) runc (default is runc-docker)
PREFERRED_PROVIDER_virtual/runc = "runc-opencontainers"
