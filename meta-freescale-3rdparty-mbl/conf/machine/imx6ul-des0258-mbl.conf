# Copyright (c) 2019 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: IMX6UL DES0258
#@DESCRIPTION: Machine configuration for the IMX6UL DES0258

MACHINEOVERRIDES =. "imx6ul-des0258:${MACHINE}:"

include conf/machine/imx6ulevk.conf
require conf/include/mbl-default.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------
SERIAL_CONSOLES = "115200;ttymxc0"

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------


##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
# PREFERRED_PROVIDER_virtual/kerne"
#   Specify which kernel recipe to use
# KERNEL_CLASSES
#   mbl-fitimage.bbclass is inherited into kernel.bbclass processing by
#   defining this symbol to include mbl-fitimage.
# KERNEL_IMAGETYPE
#   The kernel is packages in a FIT image by specifying "fitImage"
# KERNEL_DEVICETREE
#   Its unnecessary to change this symbol here as the required
#   "imx6l-des0258.dtb" value is specified in imx6l-des0258.conf (included above)
##############################################################################
#KERNEL_CLASSES  = "mbl-fitimage"
PREFERRED_PROVIDER_virtual/kernel = "linux-fslc"
KERNEL_DEVICETREE = "imx6ul-des0258.dtb"
KERNEL_DEFCONFIG = "imx6ul_des0258_defconfig"

INITRAMFS_IMAGE_BUNDLE = "1"
IMAGE_BOOT_FILES += "zImage-initramfs-imx6ul-des0258-mbl.bin"

# Bring in a boot script
MACHINE_EXTRA_RRECOMMENDS_append = "mbl-boot-scr"
IMAGE_BOOT_FILES += "boot.scr"

##############################################################################
# U-BOOT symbol definitions
#
# SPL_BINARY
#   Specify the name of U-boot SPL binary. No SPL is needed if it is empty.
##############################################################################
#SPL_BINARY=""
UBOOT_EXTLINUX = "0"

# WKS_FILES is a priority ordered list of .wks files to look for (see
# image_types_wic.bbclass). The default value of WKS_FILES has ${WKS_FILE}
# listed first so that the user can set the .wks file by setting WKS_FILE.
#
# meta-freescale-3rdparty's imx6l-des0258.conf sets WKS_FILES to
# "imx-uboot-spl.wks", but that prevents specifying the .wks file in WKS_FILE.
# Fix that here re-setting WKS_FILES to list ${WKS_FILE} first. There's not
# much point in us giving imx-uboot-spl.wks as a second item in the list (to
# use as a default .wks file for when WKS_FILE isn't set) because our BSP
# doesn't use SPL.
WKS_FILES = "${WKS_FILE}"

# Remove user-space PCI utils
MACHINE_FEATURES_remove = "pci"

# Specify the ATF recipe to be used for this machine.
PREFERRED_PROVIDER_virtual/atf = "atf-${MACHINE}"

##############################################################################
# Partition layout config
##############################################################################
MBL_WKS_BOOTLOADER1_FILENAME = "SPL"
MBL_WKS_BOOTLOADER1_OFFSET_BANK1_KiB = "1"
MBL_WKS_BOOTLOADER1_SIZE_KiB = "68"
MBL_WKS_BOOTLOADER1_IS_BL2 = "0"

MBL_WKS_BOOTLOADER2_FILENAME = "u-boot.img"
MBL_WKS_BOOTLOADER2_ALIGN_KiB = "1"
MBL_WKS_BOOTLOADER2_OFFSET_BANK1_KiB = "69"
MBL_WKS_BOOTLOADER2_SIZE_KiB = "1024"

# Guess based on value for imx6ul-pico
MBL_FLASH_ERASE_BLOCK_SIZE_KiB = "512"

# Guess based on value for imx6ul-pico
MBL_WKS_STORAGE_SIZE_MiB = "3616"

##############################################################################
# SystemD watchdog config
##############################################################################
# Set the max timeout. The imx series watchdog driver explicitly defines a max
# watchdog timeout of 128 seconds.
MBL_WATCHDOG_TIMEOUT_SECS ?= "128"
# Set the device file name for the hardware watchdog
MBL_WATCHDOG_DEVICE_FILENAME ?= "/dev/watchdog"
