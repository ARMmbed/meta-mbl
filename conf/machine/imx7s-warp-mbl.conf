#@TYPE: Machine
#@NAME: NXP WaRP7 Development Board
#@DESCRIPTION: Machine configuration for the WaRP7 board with extra features enabled

MACHINEOVERRIDES =. "imx7s-warp:${MACHINE}:"
include conf/machine/imx7s-warp.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------

MACHINE_EXTRA_RRECOMMENDS += "linux-firmware-bcm43430 linux-firmware-cyw43430a1"

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------

WKS_FILE = "mbl-imx7s-warp.wks"

IMAGE_BOOT_FILES += "u-boot.bin"
IMAGE_BOOT_FILES += "u-boot.cfg"

# Install initramfs into the boot partition
IMAGE_BOOT_FILES += "zImage-initramfs-imx7s-warp-mbl.bin"

# Combine initramfs image and kernel together to form a single image
# as it is recommended to bundle the initramfs image with the kernel image to avoid 
# circular dependencies between the kernel recipe and the initramfs recipe should 
# the initramfs image include kernel modules.
INITRAMFS_IMAGE_BUNDLE = "1"

# meta-mbl's linux-firmware bbappend (for imx7s-warp-mbl support) conflicts
# with meta-raspberrypi's linux-firmware bbappend (for rpi support). Make sure
# meta-raspberrypi's bbappend doesn't get used for imx7s-warp-mbl.
BBMASK += "/meta-raspberrypi/recipes-kernel/linux-firmware/"

# Include the ATF recipe in the build
EXTRA_IMAGEDEPENDS += "atf-warp7"
