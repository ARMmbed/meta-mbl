# Copyright (c) 2018 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: NXP IMX7D PICO-PI Development Board
#@DESCRIPTION: Machine configuration for the PICO PI board

MACHINEOVERRIDES =. "imx7d-pico:${MACHINE}:"
include conf/machine/imx7d-pico.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------
SERIAL_CONSOLES = "115200;ttymxc4"

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------

# MBL_DEBUG_INTERFACE variable is used to identify what is the network interface
# that will be used for debugging. For now, this variable is being consumed by
# the systemd-net-conf when generating systemd-net-conf-dbg-iface package.
MBL_DEBUG_INTERFACE = "usb0"

##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
# KERNEL_CLASSES
#   mbl-fitimage.bbclass is inherited into kernel.bbclass processing by
#   defining this symbol to include mbl-fitimage.
# KERNEL_IMAGETYPE
#   The kernel is packages in a FIT image by specifying "fitImage"
# KERNEL_DEVICETREE
#   Its unnecessary to change this symbol here as the required
#   "imx7s-warp.dtb" value is specified in imx7s-warp.conf (included above)
# KERNEL_MODULE_AUTOLOAD
#   Include the modules loaded at boot time
##############################################################################
KERNEL_CLASSES  = "mbl-fitimage"
KERNEL_DEVICETREE = "imx7d-pico.dtb"
KERNEL_IMAGETYPE = "fitImage"
# When usbgadget feature is present we set the g_ether module to be autoloaded
# during the startup because it will be used as the debug interface.
KERNEL_MODULE_AUTOLOAD += "${@bb.utils.contains('COMBINED_FEATURES', 'usbgadget', ' g_ether', '',d)}"

##############################################################################
# U-BOOT symbol definitions
#
# SPL_BINARY
#   Specify the name of U-boot SPL binary. No SPL is needed if it is empty.
##############################################################################
SPL_BINARY=""
UBOOT_EXTLINUX = "0"
UBOOT_CONFIG = ""
UBOOT_MACHINE = "pico-pi-imx7d_defconfig"
UBOOT_SUFFIX = "bin"
UBOOT_ENTRYPOINT = "0x80800000"
UBOOT_DTB_LOADADDRESS = "0x83000000"
UBOOT_IMAGE = "mbl-u-boot.bin"
UBOOT_SIGN_ENABLE = "1"

WKS_FILES = "${MACHINE}.wks"

# meta-mbl's linux-firmware bbappend (for imx7d-pico-mbl support) conflicts
# with meta-raspberrypi's linux-firmware bbappend (for rpi support). Make sure
# meta-raspberrypi's bbappend doesn't get used for imx7d-pico-mbl.
BBMASK += "/meta-raspberrypi/recipes-kernel/linux-firmware/"

# Specify the ATF recipe to be used for this machine.
PREFERRED_PROVIDER_virtual/atf = "atf-${MACHINE}"

# Don't use any of the firmware-imx-* packages - they're covered by a EULA that
# prevents us doing binary releases
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-brcm"
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-vpu-imx6d"
MACHINE_EXTRA_RRECOMMENDS_remove = "firmware-imx-vpu-imx6q"
PACKAGE_EXCLUDE += "firmware-imx-vpu-imx6d"
PACKAGE_EXCLUDE += "firmware-imx-vpu-imx6q"
PACKAGE_EXCLUDE += "firmware-imx-brcm"

INITRAMFS_IMAGE = "mbl-image-initramfs"
