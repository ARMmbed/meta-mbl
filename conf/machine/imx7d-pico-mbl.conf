# Copyright (c) 2018 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: NXP IMX7D PICO-PI Development Board
#@DESCRIPTION: Machine configuration for the PICO PI board

MACHINEOVERRIDES =. "imx7d-pico:${MACHINE}:"
include conf/machine/imx7d-pico.conf

# ------------------------------------------------------------------------------
# non-MBL-specific (upstreamable) configuration
# ------------------------------------------------------------------------------

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------

##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
# KERNEL_CLASSES
#   mbl-fitimage.bbclass is inherited into kernel.bbclass processing by
#   defining this symbol to include mbl-fitimage.
# KERNEL_IMAGETYPE
#   The kernel is packages in a FIT image by specifying "fitImage"
# KERNEL_DEVICETREE
#   Its unnecessary to change this symbol here as the required
#   "imx7s-warp.dtb" value is specified in imx7s-warp.conf (included above)
##############################################################################
KERNEL_IMAGETYPE = "uImage"
KERNEL_DEVICETREE = "imx7d-pico.dtb"

##############################################################################
# U-BOOT symbol definitions
#
# SPL_BINARY
#   Specify the name of U-boot SPL binary. No SPL is needed if it is empty.
##############################################################################
SPL_BINARY=""
UBOOT_ENTRYPOINT = "0x80800000"
UBOOT_DTB_LOADADDRESS = "0x83000000"

UBOOT_EXTLINUX = "0"
UBOOT_CONFIG = ""
UBOOT_MACHINE = "pico-pi-imx7d_defconfig"
UBOOT_SUFFIX = "bin"

WKS_FILES = "${MACHINE}.wks"

# Combine initramfs image and kernel together to form a single image
# as it is recommended to bundle the initramfs image with the kernel image to avoid
# circular dependencies between the kernel recipe and the initramfs recipe should
# the initramfs image include kernel modules.
INITRAMFS_IMAGE_BUNDLE = "1"

# Install the initramfs image bundle including kernel into the boot partition.
IMAGE_BOOT_FILES += "uImage-initramfs-${MACHINE}.bin"

# meta-mbl's linux-firmware bbappend (for imx7d-pico-mbl support) conflicts
# with meta-raspberrypi's linux-firmware bbappend (for rpi support). Make sure
# meta-raspberrypi's bbappend doesn't get used for imx7d-pico-mbl.
BBMASK += "/meta-raspberrypi/recipes-kernel/linux-firmware/"

# This dependency is a temporary measure and will be removed when the
# imx7d-pico-mbl BSP adopts mbl-fitimage.bbclass for preparing the
# FIT image, which installs boot.scr in MBL_FIT_IMAGE_BOOT_FILES_DIR.
# However, until that time, the boot.scr (FIT image) is added directly
# to IMAGE_BOOT_FILES.
IMAGE_BOOT_FILES += "boot.scr"

# Specify the ATF recipe to be used for this machine.
PREFERRED_PROVIDER_virtual/atf = "atf-${MACHINE}"

# This dependency is a temporary measure and will be removed when the
# imx7d-pico-mbl BSP adopts an atf-${MACHINE} recipe that includes
# atf.inc (atf.inc includes the dependency on virtual/bootloader).
# The image recipes (e.g. mbl-console-image) have
# do_image_wic[depends] += "virtual/atf:do_deploy"
do_image_wic[depends] += "virtual/bootloader:do_deploy"

# This dependency is a temporary measure and will be removed when the
# imx7d-pico-mbl BSP adopts mbl-fitimage.bbclass to create the FIT
# image. However, at the present time mbl-boot-scr.bb(append) are used to
# create the FIT image and hence the following dependency is present.
do_image_wic[depends] += "mbl-boot-scr:do_deploy"
