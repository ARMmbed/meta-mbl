# Copyright (c) 2019 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: NXP i.MX8MM Evaluation Kit
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX8MMini EVK with extra features enabled

MACHINEOVERRIDES =. "imx8mmevk:${MACHINE}:"
include conf/machine/imx8mmevk.conf

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------


##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
##############################################################################
# Not supported in the 4.14 FSL kernel tree
KERNEL_DEVICETREE_remove = "freescale/fsl-imx8mm-evk-inmate.dtb "

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements a recipe to create a FIT image consistent
# with the MBL bootflow.
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-imx"

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements a recipe to apply MBL specific changes and
# additions.
PREFERRED_PROVIDER_virtual/kernel = "linux-imx"

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements atf-${MACHINE}.bb to create the FIP image.
# However, until that time, the meta-freescale ATF recipe is used.
PREFERRED_PROVIDER_virtual/atf = "imx-atf"

# define wks
WKS_FILE = "${MACHINE}.wks"

# imx-boot package - pulls in firmware binaries for training DDR
MACHINE_EXTRA_RRECOMMENDS += " imx-boot"

# This directive is a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass for preparing the
# FIT image, which installs boot.scr in MBL_FIT_IMAGE_BOOT_FILES_DIR.
# However, until that time, the boot.scr (FIT image) is added directly
# to IMAGE_BOOT_FILES.
IMAGE_BOOT_FILES += "boot.scr"

# These two directives are a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass.
# When we move to fit the kernel Image and initramfs will be individually
# crypotgraphically signed inside of the FIT.
# Here we remove the standalone kernel Image and instead include the
# concatonated Kernel + Initramfs.
# A corresponding change to boot.scr points u-boot $image to
# "Image-initramfs-imx8mmevk-mbl.bin" instead of "Image".
IMAGE_BOOT_FILES_remove = "Image"
IMAGE_BOOT_FILES += "Image-initramfs-imx8mmevk-mbl.bin"

# This dependency is a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass to create the FIT
# image. However, at the present time mbl-boot-scr.bbappend is used to
# create the FIT image and hence the following dependency is present.
do_image_wic[depends] += "mbl-boot-scr:do_deploy"

BBMASK += "/meta-raspberrypi/recipes-kernel/linux-firmware/"

INITRAMFS_IMAGE = "mbl-image-initramfs"

# Add in WiFi driver and firmware from meta-freescale-bsp-release
BBFILES += "${OEROOT}/layers/meta-fsl-bsp-release/imx/meta-bsp/recipes-kernel/kernel-modules/kernel-module-qca9377_2.1.bb \
            ${OEROOT}/layers/meta-fsl-bsp-release/imx/meta-bsp/recipes-kernel/kernel-modules/kernel-module-qcacld-lea.inc"
MACHINE_EXTRA_RRECOMMENDS_append = " kernel-module-qca9377 "
