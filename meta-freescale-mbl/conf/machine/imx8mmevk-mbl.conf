# Copyright (c) 2019 Arm Limited and Contributors. All rights reserved.
#
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: NXP i.MX8MM Evaluation Kit
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX8MMini EVK with extra features enabled

MACHINEOVERRIDES =. "imx8mmevk:${MACHINE}:"
include conf/machine/imx8mmevk.conf
require conf/include/mbl-default.conf

# ------------------------------------------------------------------------------
# MBL-specific (non-upstreamable) configuration
# ------------------------------------------------------------------------------

##############################################################################
# KERNEL symbol definitions for virtual/kernel build configuration
#
##############################################################################
# Not supported in the 4.14 FSL kernel tree
KERNEL_DEVICETREE_remove = "freescale/fsl-imx8mm-evk-inmate.dtb "

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements a recipe to create a FIT image consistent
# with the MBL bootflow.
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-imx"

##############################################################################
# U-BOOT symbol definitions
#
# SPL_BINARY
#   Specify the name of U-boot SPL binary. No SPL is needed if it is empty.
# UBOOT_CONFIG[sd]
#   Refers to the MBL specific u-boot config for sdcard
#   necessary for any differentation required @ u-boot level for mbed Linux.
##############################################################################
UBOOT_SUFFIX = "bin"
UBOOT_BINARY = "u-boot.${UBOOT_SUFFIX}"
UBOOT_CONFIG[sd] = "imx8mm_evk_mbl_config,sdcard"

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements a recipe to apply MBL specific changes and
# additions.
PREFERRED_PROVIDER_virtual/kernel = "linux-imx"

# This directive is a temporary measure and will be changed when the
# imx8mmevk-mbl BSP implements atf-${MACHINE}.bb to create the FIP image.
# However, until that time, the meta-freescale ATF recipe is used.
PREFERRED_PROVIDER_virtual/atf = "imx-atf"

# imx-boot package - pulls in firmware binaries for training DDR
MACHINE_EXTRA_RRECOMMENDS += " imx-boot"

# This directive is a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass for preparing the
# FIT image, which installs boot.scr in MBL_FIT_IMAGE_BOOT_FILES_DIR.
# However, until that time, the boot.scr (FIT image) is added directly
# to IMAGE_BOOT_FILES.
IMAGE_BOOT_FILES += "boot.scr"

# These two directives are a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass.
# When we move to fit the kernel Image and initramfs will be individually
# crypotgraphically signed inside of the FIT.
# Here we remove the standalone kernel Image and instead include the
# concatonated Kernel + Initramfs.
# A corresponding change to boot.scr points u-boot $image to
# "Image-initramfs-imx8mmevk-mbl.bin" instead of "Image".
IMAGE_BOOT_FILES_remove = "Image"
IMAGE_BOOT_FILES += "Image-initramfs-imx8mmevk-mbl.bin"

# This dependency is a temporary measure and will be removed when the
# imx8mmevk-mbl BSP adopts mbl-fitimage.bbclass to create the FIT
# image. However, at the present time mbl-boot-scr.bbappend is used to
# create the FIT image and hence the following dependency is present.
do_image_wic[depends] += "mbl-boot-scr:do_deploy"

BBMASK += "/meta-raspberrypi/recipes-kernel/linux-firmware/"

INITRAMFS_IMAGE_BUNDLE = "1"

# Remove the Qualcomm Wifi firmware (qca*) RRECOMMENDS due to license restrictions
MACHINE_EXTRA_RRECOMMENDS_remove = "packagegroup-fsl-qca9377 packagegroup-fsl-qca6174"
